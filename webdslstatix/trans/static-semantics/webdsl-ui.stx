module static-semantics/webdsl-ui

imports
  static-semantics/actions/functions

  static-semantics/entities/annotations

  static-semantics/types/built-ins

  static-semantics/ui/actions
  static-semantics/ui/ajax
  static-semantics/ui/attributes
  static-semantics/ui/built-ins
  static-semantics/ui/template-calls

  static-semantics/webdsl-actions
  static-semantics/webdsl-types
  static-semantics/webdsl

rules // page definitions

  defOk(s_global, TemplateDefinition(DefinePage(mods, x, FormalArgs(fargs), TemplateArgs(targs), elements))) :- {fargTypes s_page s_body}
    // TO-DO: analyse mods
    targs == [] | error $[Template arguments are not allowed on page definitions] @targs,
    new s_page, s_page -F-> s_global,
    fargTypes == typesOfArgs(s_global, fargs),
    declareParameters(s_page, zipArgTypes(fargs, fargTypes)),
    new s_body, s_body -P-> s_page,
    declarePage(s_global, x, fargTypes),
    templateElementOrderOk(elements, FALSE(), FALSE()),
    templateElementsOk(s_body, elements).

rules // template definitions

  defOk(s, TemplateDefinition(td@DefineTemplate(_, _, _, _, _))) :- defineTemplateOk(s, td, TRUE()).

  defineTemplateOk : scope * TemplateDefinition * BOOL
  defineTemplateOk(s, DefineTemplate(mods, t, FormalArgs(fargs), TemplateArgs(targs), elements), declare) :- {fargTypes targTypes s_template s_body}
    // TO-DO: analyse mods
    new s_template, s_template -F-> s,
    fargTypes == typesOfArgs(s, fargs),
    targTypes == typesOfTemplateArgs(s, targs),
    declareParameters(s_template, zipArgTypes(fargs, fargTypes)),
    declTArgs(s_template, targTypes),
    new s_body, s_body -P-> s_template,
    optionallyDeclareTemplate(s, t, fargTypes, targTypes, declare),
    templateElementOrderOk(elements, FALSE(), FALSE()),
    templateElementsOk(s_body, elements).

  optionallyDeclareTemplate : scope * string * list(TYPE) * list((string * list(TYPE))) * BOOL
  optionallyDeclareTemplate(_, _, _, _, FALSE()).
  optionallyDeclareTemplate(s, t, fargTypes, targTypes, TRUE()) :- declTemplate(s, t, fargTypes, targTypes).

  declTemplate : scope * string * list(TYPE) * list((string * list(TYPE)))
  declTemplate(s, t, fargTypes, targTypes) :- {result}
    declareTemplate(s, t, fargTypes),
    resolveTemplate(s, t) == result,
    amountOfTemplateDeclsWithArgs(result, fargTypes, 0) == 1
      | error $[Duplicate definition of template [t] with argument types [fargTypes]] @t.

  declTArgs maps declTArg(*, list(*))
  declTArg : scope * (string * list(TYPE))
  declTArg(s, (t, argTypes)) :-
    declTemplate(s, t, argTypes, []).

  // helper function that results in the amount of template definitions with the given argument types
  amountOfTemplateDeclsWithArgs : list((path *(string * TYPE))) * list(TYPE) * int -> int
  amountOfTemplateDeclsWithArgs([], _, n) = n.
  amountOfTemplateDeclsWithArgs([ (_, (_, TEMPLATE(args))) | tail], args, n) = amountOfTemplateDeclsWithArgs(tail, args, i) :- i #= n + 1.
  amountOfTemplateDeclsWithArgs([ _ | tail], args, n) = amountOfTemplateDeclsWithArgs(tail, args, n).

rules // template elements

  templateElementOk : scope * scope * TemplateElement // TYPE argument is the return type in a function
  templateElementOk(_, _, e) :- try { false } | warning $[This template element is not yet implemented] @e.

  templateElementsOk : scope * list(TemplateElement)
  templateElementsOk(_, []).
  templateElementsOk(s, [element | tail]) :- {s_next}
    new s_next, s_next -P-> s,
    s_next -T-> s, s -T-> s_next, // create forward and backward template edges to resolve placeholders or actions defined later
    templateElementOk(s, s_next, element),
    templateElementsOk(s_next, tail).

  templateElementOk(s, _, Text(parts)) :-
    stringPartsOk(s, parts).

  templateElementOk(s, _, XMLComment(_)).

  templateElementOk(s, _, XMLEmptyElement(_, attributes)) :-
    attributesOk(s, attributes).

  templateElementOk(s, _, XMLEmptyElementUnclosed(_, attributes)) :-
    attributesOk(s, attributes).

  templateElementOk(s, _, XMLElement(openTagName, attributes, elements, closeTagName)) :-
    openTagName == closeTagName | error $[Opening and closing tag do not match],
    attributesOk(s, attributes),
    templateElementsOk(s, elements).

  templateElementOk(s, _, JSElement(attributes, parts)) :-
    attributesOk(s, attributes),
    jsStringPartsOk(s, parts).

  templateElementOk(s, _, StyleElement(attributes, parts)) :-
    attributesOk(s, attributes),
    jsStringPartsOk(s, parts).

  jsStringPartsOk maps jsStringPartOk(*, list(*))
  jsStringPartOk : scope * JSPart
  jsStringPartOk(s, JSString(_)).
  jsStringPartOk(s, JSExp(exp)) :- typed(s, exp).

  templateElementOk(s, _, IfTempl(c, then, else)) :-
    equalType(typeOfExp(s, c), bool(s)) | error $[Condition should be of type bool] @c,
    templateElementsOk(s, then),
    templateElementsOk(s, else).

  templateElementOk(s, _, TemplateDefinition2TemplateElement(td)) :- defineTemplateOk(s, td, FALSE()).

  templateElementOk(s, _, SubmitElem(a, props, elems)) :-
    actionCallOrInlineOk(s, a),
    templatePropAssignmentsOk(s, props),
    templateElementsOk(s, elems).

  templateElementOk(s, _, SubmitLinkElem(a, props, elems)) :-
    actionCallOrInlineOk(s, a),
    templatePropAssignmentsOk(s, props),
    templateElementsOk(s, elems).

  templateElementOk(s, _, DownloadLink(a, props, elems)) :-
    actionCallOrInlineOk(s, a),
    templatePropAssignmentsOk(s, props),
    templateElementsOk(s, elems).

  templateElementOk(s, _, OutputImage(a, props, elems)) :-
    actionCallOrInlineOk(s, a),
    templatePropAssignmentsOk(s, props),
    templateElementsOk(s, elems).

rules // expressions

  typeOfExp(s, TemplateName2Exp(TemplateName())) = string(s). // TO-DO: only allow this if inside a template

rules // loops

  templateElementOk(s, _, For(x, srt, exp, OptFilterSome(f), elems, ForSeparator(separatorElems))) :- {s_for typeOfSort typeOfExp}
    typeOfSort == typeOfSort(s, srt),
    typeOfExp == typeOfExp(s, exp),
    or(
      equalTypeB(LIST(typeOfSort), typeOfExp),
      equalTypeB(SET(typeOfSort), typeOfExp)
    ) | error $[Must be a list or set of type [typeOfSort], [typeOfExp] given] @exp,
    new s_for, s_for -P-> s,
    forLoopFilterOk(s_for, f),
    forLoopOk(s_for, x, typeOfSort, elems),
    separatorOk(s, separatorElems),
    @x.type := typeOfSort.

  templateElementOk(s, _, ForInferred(x, exp, OptFilterSome(f), elems, ForSeparator(separatorElems))) :- {s_for typeOfExp t}
    typeOfExp == typeOfExp(s, exp),
    t == stripGenericType(typeOfExp),
    new s_for, s_for -P-> s,
    forLoopFilterOk(s_for, f),
    forLoopOk(s_for, x, t, elems),
    separatorOk(s, separatorElems),
    @x.type := t.

  templateElementOk(s, _, ForAll(x, srt, OptFilterSome(f), elems, ForSeparator(separatorElems))) :- {s_for typeOfSort}
    typeOfSort == typeOfSort(s, srt),
    new s_for, s_for -P-> s,
    forLoopFilterOk(s_for, f),
    forLoopOk(s_for, x, typeOfSort, elems),
    separatorOk(s, separatorElems),
    @x.type := typeOfSort.

  templateElementOk(s, _, ForCount(x, fromExp, toExp, elems, ForSeparator(separatorElems))) :- {s_for intType}
    intType == int(s),
    equalType(typeOfExp(s, fromExp), intType) | error $[From expression should have type Int],
    equalType(typeOfExp(s, toExp)  , intType) | error $[To expression should have type Int],
    new s_for, s_for -P-> s,
    forLoopOk(s_for, x, intType, elems),
    separatorOk(s, separatorElems),
    @x.type := intType.

  forLoopOk : scope * string * TYPE * list(TemplateElement)
  forLoopOk(s_for, x, t, stmts) :-
    declareVar(s_for, x, t),
    declareAnnotation(s_for, x, DERIVED()), // abuse derived annotation to declare immutability of x
    templateElementsOk(s_for, stmts).

  separatorOk : scope * list(TemplateElement)
  separatorOk(s, elems) :- {s_separator}
    new s_separator, s_separator -P-> s,
    templateElementsOk(s_separator, elems).

rules // enforce correct order of template elements

  // correct order:
  // 1. var decls
  // 2. init block
  // 3. other template elements
  templateElementOrderOk : list(TemplateElement) * BOOL * BOOL
  templateElementOrderOk([], _, _).
  templateElementOrderOk([x | xs], _, _). // TO-DO: verify if we want this check :- templateElementOrderOk(xs, TRUE(), TRUE()).

  templateElementOrderOk([TEVarDecl(v) | xs], varDeclsDone, initDone) :-
    varDeclsDone == FALSE() | error $[Variable declarations must be at the start of a page or template] @v,
    templateElementOrderOk(xs, varDeclsDone, initDone).

  templateElementOrderOk([i@Init(_) | xs], _, initDone) :-
    initDone == FALSE() | error $[Init block must be between variable declarations and the rest of the page or template] @i,
    templateElementOrderOk(xs, TRUE(), initDone).

rules // utils

  rootPageDefined : scope * string
  rootPageDefined(s, errorLoc) :-
    resolvePage(s, "root") == [(_, (_, PAGE(_)))|_] | error $[No root page defined] @errorLoc.

  typesOfTemplateArgs maps typeOfTemplateArg(*, list(*)) = list(*)
  typeOfTemplateArg : scope * TemplateArg -> (string * list(TYPE))
  typeOfTemplateArg(s, TemplateArg(x, argSorts)) = (x, typesOfSorts(s, argSorts)).
