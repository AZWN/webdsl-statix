module static-semantics/ui/ajax

imports
  static-semantics/types/built-ins

  static-semantics/ui/template-calls

  static-semantics/webdsl-ui
  static-semantics/webdsl

rules // placeholders and ajax

  templateElementOk(s, _, TEPlaceholder(PlaceholderHtml(_, ph, elems))) :-
    declarePlaceholder(s, ph),
    templateElementsOk(s, elems).

  templateElementOk(s, _, TEPlaceholder(PlaceholderHtmlExp(_, e, elems))) :-
    typeCompatible(typeOfExp(s, e), string(s)) | error $[Expression should be compatible with String],
    templateElementsOk(s, elems).

  templateElementOk(s, _, TEPlaceholder(Placeholder(ph, elems))) :-
    declarePlaceholder(s, ph),
    templateElementsOk(s, elems).

  templateElementOk(s, _, TEPlaceholder(PlaceholderExp(e, elems))) :-
    typeCompatible(typeOfExp(s, e), string(s)) | error $[Expression should be compatible with String],
    templateElementsOk(s, elems).

  templateElementOk(s, _, TEPlaceholderWithAjaxCall(PlaceholderAjaxHtml(_, ph, tc))) :-
    declarePlaceholder(s, ph),
    templateCallOk(s, tc).

  templateElementOk(s, _, TEPlaceholderWithAjaxCall(PlaceholderAjaxHtmlExp(_, e, tc))) :-
    typeCompatible(typeOfPlaceholderExp(s, e), string(s)) | error $[Expression should be compatible with String],
    templateCallOk(s, tc).

  templateElementOk(s, _, TEPlaceholderWithAjaxCall(PlaceholderAjax(ph, tc))) :-
    declarePlaceholder(s, ph),
    templateCallOk(s, tc).

  templateElementOk(s, _, TEPlaceholderWithAjaxCall(PlaceholderAjaxExp(e, tc))) :-
    typeCompatible(typeOfPlaceholderExp(s, e), string(s)) | error $[Expression should be compatible with String],
    templateCallOk(s, tc).

rules // ajax statements

  stmtOk(s, _, AjaxStatement(stmt), _) :- ajaxStmtOk(s, stmt).

  ajaxStmtOk : scope * AjaxStatement
  ajaxStmtOk(_, _) :-
    try { false } | warning $[This ajax statement is not yet implemented].

  ajaxStmtOk(s, AjaxReplace(e, tc)) :-
    ajaxPlaceholderExpressionOk(s, e),
    ajaxTemplateCallOk(s, tc)
      | error $[Unable to find an ajax enabled template with a compatible signature] @tc.

  ajaxStmtOk(s, AjaxAppend(e, tc)) :-
    ajaxPlaceholderExpressionOk(s, e),
    ajaxTemplateCallOk(s, tc)
      | error $[Unable to find an ajax enabled template with a compatible signature] @tc.

  ajaxStmtOk(s, AjaxVisibility(e, _)) :-
    ajaxPlaceholderExpressionOk(s, e).

  ajaxStmtOk(s, AjaxRelocate(pc)) :-
    pageCallOk(s, pc).

  ajaxStmtOk(s, AjaxRestyle(ph, e)) :-
    ajaxPlaceholderExpressionOk(s, e),
    typeCompatible(typeOfExp(s, e), string(s)) | error $[CSS classname must be compatible with type String].

  ajaxStmtOk(s, AjaxClear(ph)) :-
    ajaxPlaceholderExpressionOk(s, ph).

  ajaxStmtOk(s, AjaxRefresh()).

  ajaxStmtOk(s, AjaxRunScript(e)) :-
    typeCompatible(typeOfExp(s, e), string(s)) | error $[Javascript code must be compatible with type String].

  ajaxPlaceholderExpressionOk : scope * Exp
  ajaxPlaceholderExpressionOk(s, e) :-
    typeCompatible(typeOfExp(s, e), string(s))
      | error $[Expression should be compatible with type string or a placeholder variable] @e.

  ajaxPlaceholderExpressionOk(s, Var(ph)) :- { ph' }
    resolvePlaceholder(s, ph) == [(_, ph') | _] | error $[Unable to find placeholder [ph]],
    @ph.ref := ph'.
