module static-semantics/types/built-ins

imports
  static-semantics/webdsl-entities
  static-semantics/webdsl-native
  static-semantics/webdsl-types
  static-semantics/webdsl

rules

  declareBuiltInType : scope * string
  declareBuiltInType(s, name) :- {s_type}
    new s_type, s_type -IMPORT-> s,
    declareExtendEntity(s, name, s_type), // declare s_type to be linked to type name
    extendEntityScopes(resolveExtendEntity(s, name), s_type),
    declareType(s, name, BUILTINTYPE(name, s_type)),
    declareVar(s, name, STATICBUILTINTYPE(name, s_type)). // declare class name as global variable that refers to the static scope

  declareBuiltInSubType : scope * string * string
  declareBuiltInSubType(s, sub, super) :- {s_sub s_super}
    builtInType(s, super) == BUILTINTYPE(super, s_super),
    new s_sub, s_sub -INHERIT-> s_super,
    noCircularInheritance(s_sub) | error $[Circular inheritance detected in built-in type [sub]],
    declareExtendEntity(s, sub, s_sub), // declare s_type to be linked to type name
    extendEntityScopes(resolveExtendEntity(s, sub), s_sub),
    declareType(s, sub, BUILTINTYPE(sub, s_sub)),
    declareVar(s, sub, STATICBUILTINTYPE(sub, s_sub)).

  builtInType : scope * string -> TYPE
  builtInType(s, name) = t :-
    resolveType(s, name) == [(_, (_, t@BUILTINTYPE(name, _)))] | error $[Built-in type [name] does not exist].

rules

  int   : scope -> TYPE
  int(s)   = builtInType(s, "Int").
  float : scope -> TYPE
  float(s) = builtInType(s, "Float").
  long  : scope -> TYPE
  long(s)  = builtInType(s, "Long").

  string   : scope -> TYPE
  string(s)   = builtInType(s, "String").
  secret   : scope -> TYPE
  secret(s)   = builtInType(s, "Secret").
  email    : scope -> TYPE
  email(s)    = builtInType(s, "Email").
  url      : scope -> TYPE
  url(s)      = builtInType(s, "URL").
  wikitext : scope -> TYPE
  wikitext(s) = builtInType(s, "WikiText").
  text     : scope -> TYPE
  text(s)     = builtInType(s, "Text").
  patch    : scope -> TYPE
  patch(s)    = builtInType(s, "Patch").

  date     : scope -> TYPE
  date(s)     = builtInType(s, "Date").
  datetime : scope -> TYPE
  datetime(s) = builtInType(s, "DateTime").
  time     : scope -> TYPE
  time(s)     = builtInType(s, "Time").

  bool  : scope -> TYPE
  bool(s)  = builtInType(s, "Bool").
  uuid  : scope -> TYPE
  uuid(s)  = builtInType(s, "UUID").
  file  : scope -> TYPE
  file(s)  = builtInType(s, "File").
  image : scope -> TYPE
  image(s) = builtInType(s, "Image").

  declareTypeBuiltIns : scope
  declareTypeBuiltIns(s) :-
    // numeric types
    declareBuiltInType(s, "Int"),
    declareBuiltInType(s, "Float"),
    declareBuiltInType(s, "Long"),

    // string types
    declareBuiltInType(s, "String"),
    declareBuiltInType(s, "Secret"),
    declareBuiltInType(s, "Email"),
    declareBuiltInType(s, "URL"),
    declareBuiltInType(s, "WikiText"),
    declareBuiltInType(s, "Text"),
    declareBuiltInType(s, "Patch"),

    // date types
    declareBuiltInType(s, "DateTime"),
    declareBuiltInSubType(s, "Date", "DateTime"),
    declareBuiltInSubType(s, "Time", "DateTime"),

    // misc
    declareBuiltInType(s, "Bool"),
    declareBuiltInType(s, "UUID"),
    declareBuiltInType(s, "File"),
    declareBuiltInType(s, "Image").
