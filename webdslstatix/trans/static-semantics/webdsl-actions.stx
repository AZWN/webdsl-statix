module static-semantics/webdsl-actions

imports
  static-semantics/actions/binops
  static-semantics/webdsl-types
  static-semantics/webdsl

rules // functions

  defOk(s, GlobalFunction(f)) :- functionOk(s, f).
  defOk(s, ExtendGlobalFunction(f)) :- functionOk(s, f).
  defOk(s, CachedGlobalFunction(f)) :-
    canBeCached(s, f),
    functionOk(s, f).

  functionOk : scope * Function
  functionOk(s_outer, Function(_, FormalArgs(args), OptSortSome(returnSort), Block(stmts))) :- {returnType s_function s_body}
    returnType == typeOfSort(s_outer, returnSort),
    new s_function, s_function -F-> s_outer,
    declArgs(s_function, args),
    new s_body, s_body -P-> s_function,
    stmtsOk(s_body, stmts, returnType).

  declArgs maps declArg(*, list(*))
  declArg : scope * FormalArg
  declArg(s, Arg(x, sort)) :- {t}
    t == typeOfSort(s, sort),
    inequalType(t, UNTYPED()) | error $[Unknown type [sort]] @sort,
    declVar(s, x, t).

  canBeCached : scope * Function
  canBeCached(s, Function(function_name, FormalArgs([]), OptSortSome(returnSort), _)) :- 
    inequalType(typeOfSort(s, returnSort), VOID())
        | error $[Only functions that have a return type can be cached] @function_name.

  canBeCached(s, Function(function_name, FormalArgs([_|_]), _, _)) :- false
        | error $[Only functions without arguments can be cached] @function_name.

rules // statements

  stmtOk(s, Stat(exp), _) :-
    expOk(s, exp).

  stmtOk(s, VarDeclInit(x, sort, exp), _) :- {t}
    t == typeOfSort(s, sort),
    equalType(t, typeOfExp(s, exp)) | error $[Expression [exp] is not of type [sort]] @exp,
    declVar(s, x, t).

  stmtOk(s, Assignment(_, exp), _) :- false | error $[Left-hand side of assignment should be a variable].
  stmtOk(s, Assignment(Var(x), exp), _) :- {varType}
    varType == getVariableType(s, x),
    equalType(varType, typeOfExp(s, exp)) | error $[Expression [exp] is not of type [varType]].

  stmtOk(s, If(c, Block(then), Block(else)), rt) :- {s_then s_else}
    equalType(typeOfExp(s, c), BOOL()) | error $[Condition should be of type bool],
    new s_then, s_then -P-> s,
    stmtsOk(s_then, then, rt),
    new s_else, s_else -P-> s,
    stmtsOk(s_else, else, rt).

  stmtOk(s, r@Return(exp), returnType) :- {returnExpType}
    inequalType(returnType, UNTYPED()) | error $[Return statement not allowed here] @r,
    inequalType(returnType, VOID()) | error $[No return type specified in this context] @exp,
    returnExpType == typeOfExp(s, exp),
    equalType(returnExpType, returnType) | error $[Must return an expression of type [returnType], [returnExpType] given] @exp.

  stmtOk(s, r@ReturnEmpty(), returnType) :-
    inequalType(returnType, UNTYPED()) | error $[Return statement not allowed here] @r,
    equalType(returnType, VOID()) | error $[Must return an expression of type [returnType]] @r.

rules // variables

  typeOfExp(s, Var(x)) = getVariableType(s, x).

  defOk(s, GlobalVarDecl(x, sort)) :- {t}
    t == typeOfSort(s, sort),
    declVar(s, x, t).

  defOk(s, GlobalVarDeclInit(x, sort, exp)) :- {t}
    t == typeOfSort(s, sort),
    equalType(t, typeOfExp(s, exp)) | error $[Expression [exp] is not of type [sort]],
    declVar(s, x, t).

  defOk(s, GlobalVarDeclInitInferred(x, exp)) :- {t}
    t == typeOfExp(s, exp),
    inequalType(t, UNTYPED()) | error $[Cannot resolve type of expression [exp]],
    declVar(s, x, t).

  declVar : scope * string * TYPE
  declVar(s, x, t) :-
    s -> Variable{x} with typeOfDecl t,
    noDuplicateVarDefs(s, x) | error $[Duplicate defition of [x] in this context].

  noDuplicateVarDefs : scope * string
  noDuplicateVarDefs(s, x) :-
    query typeOfDecl filter P* and { Variable{x'} :- x' == x }
                     min $ < P
                     in s |-> [_].

  noDuplicateVarDefsInSuper : scope * string
  noDuplicateVarDefsInSuper(s_sub, x) :-
    query typeOfDecl filter EXTEND? (INHERIT EXTEND?)* and { Variable{x'} :- x' == x }
                     min $ < EXTEND, $ < INHERIT, EXTEND < INHERIT
                     in s_sub |-> [_].
