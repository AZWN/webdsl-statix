module static-semantics/webdsl-actions

imports
  static-semantics/actions/binops
  static-semantics/webdsl-types
  static-semantics/webdsl

rules // functions

  defOk(s, GlobalFunction(f)) :- functionOk(s, f).

  functionOk : scope * Function
  functionOk(s, Function(_, args, returntype, Block(stmts))) :- {s_function}
    new s_function, s_function -INHERIT-> s,
    stmtsOk(s_function, stmts).

rules // statements

  stmtOk(s, Stat(exp)) :-
    expOk(s, exp).

rules // variables

  typeOfExp(s, Var(Id2VarId(x))) = getVariableType(s, x).

  defOk(s, GlobalVarDecl2Definition(GlobalVarDeclInit(x, sort, exp))) :- {sortType}
    sortType == typeOfSort(s, sort),
    equalType(sortType, typeOfExp(s, exp)) | error $[Expression [exp] is not of type [sort]],
    declVar(s, x, sortType).

  declVar : scope * string * TYPE
  declVar(s, x, t) :-
    s -> Variable{x} with typeOfDecl t,
    noDuplicateVarDefs(s, x) | error $[Duplicate defition of [x] in this context].

  noDuplicateVarDefs : scope * string
  noDuplicateVarDefs(s, x) :-
    query typeOfDecl filter P* and { Variable{x'} :- x' == x }
                     min $ < P
                     in s |-> [_].

  noDuplicateVarDefsInSuper : scope * string
  noDuplicateVarDefsInSuper(s_sub, x) :-
    query typeOfDecl filter P* INHERIT* and { Variable{x'} :- x' == x }
                     min $ < P, $ < INHERIT, P < INHERIT
                     in s_sub |-> [_].

  getVariableType : scope * string -> TYPE
  getVariableType(s, x) = t :-
    typeOfDecl of Variable{x} in s |-> [(_, (_, t))|_] | error $[Variable [x] is not defined].
