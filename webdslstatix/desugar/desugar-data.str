module desugar/desugar-data

imports
  src-gen/signatures/-

  desugar/utils

rules

  normalize-data : OptPropAnnosNone() -> PropAnnos([])
  normalize-data : ListSort(x) -> GenericSort("List", [x])
  normalize-data : SetSort(x) -> GenericSort("Set", [x])
  normalize-data : FunctionSortParamsNone() -> FunctionSortParams([])
  normalize-data : FunctionSortReturnNone() -> FunctionSortReturn(SimpleSort("Void"))

  normalize-data : GlobalVarDeclDepr(x, sort) -> GlobalVarDecl(x, sort)
  normalize-data : GlobalVarDeclInitDepr(x, sort, exp) -> GlobalVarDeclInit(x, sort, exp)
  normalize-data : GlobalVarDeclInitInferredDepr(x, exp) -> GlobalVarDeclInitInferred(x, exp)

  normalize-data : RequestVarDeclDepr(x, sort) -> RequestVarDecl(x, sort)
  normalize-data : RequestVarDeclInitDepr(x, sort, exp) -> RequestVarDeclInit(x, sort, exp)
  normalize-data : RequestVarDeclInitInferredDepr(x, exp) -> RequestVarDeclInitInferred(x, exp)

  normalize-data : Property(x, k, srt, PropAnnos(annos)) -> Property(x, k, srt, PropAnnos(annos'))
    where   <not(fetch-elem(?NameAnno()))> annos
          ; <?"name"> x
    with  annos' := [NameAnno() | annos]

  normalize-data : Entity(entity_name, super, defs) -> Entity(entity_name, super, defs')
    where
        all-function := StaticEntityFunction(Function("all", FormalArgs([]), OptSortSome(GenericSort("List", [SimpleSort(entity_name)])), Block([])))
      ; validate-save-function := EntityFunction(Function("validateSave", FormalArgs([]), OptSortSome(SimpleSort("ValidationExceptionMultiple")), Block([])))
      ; <not(fetch(?all-function))> defs
    with
        defs' := [all-function | [validate-save-function | defs]]

  normalize-data : EntityNoSuper(entity_name, defs) -> EntityNoSuper(entity_name, defs')
    where
        all-function := StaticEntityFunction(Function("all", FormalArgs([]), OptSortSome(GenericSort("List", [SimpleSort(entity_name)])), Block([])))
      ; validate-save-function := EntityFunction(Function("validateSave", FormalArgs([]), OptSortSome(SimpleSort("ValidationExceptionMultiple")), Block([])))
      ; <not(fetch(?all-function))> defs
    with
        defs' := [all-function | [validate-save-function | defs]]

  normalize-data : Entity(e, super, defs) -> Entity(e, super, defs')
    where
          props := <filter(properties-without-validate-elems(|defs))> defs
        ; <not(?0)> <length> props
    with
          new-validate-elems := <map(construct-prop-validate-elems)> props
        ; defs' := <conc> (defs, new-validate-elems)

  normalize-data : EntityNoSuper(e, defs) -> EntityNoSuper(e, defs')
    where
          props := <filter(properties-without-validate-elems(|defs))> defs
        ; <not(?0)> <length> props
    with
          new-validate-elems := <map(construct-prop-validate-elems)> props
        ; defs' := <conc> (defs, new-validate-elems)

  normalize-data : ExtendSessionEntity(ident, entbodydecs){anno*} -> ExtendEntity(ident, entbodydecs){anno*}

  normalize-data: Property(x, k, srt, PropAnnos(annos)) -> Property(x, k, srt, PropAnnos(annos'))
    where <fetch-elem(?IncompleteInverseAnno(prop))> annos
    with  ent := <try(?GenericSort(_,[<id>])); ?SimpleSort(<id>)> srt
        ; annos' := <map(try(\IncompleteInverseAnno(prop) -> InverseAnno(ent, prop)\))> annos

rules // utils

  properties-without-validate-elems(|defs) : e@Property(x, _, _, _) -> e
    where
          f := <validate-function-for-name> x
        ; <not(fetch(?f))> defs

  construct-prop-validate-elems : Property(x, _, _, _) -> f
    with
        f := <validate-function-for-name> x

  validate-function-for-name : x -> EntityFunction(Function(validate-name, FormalArgs([]), OptSortSome(SimpleSort("ValidationExceptionMultiple")), Block([])))
    with
        validate-name := <concat-strings> ["validate", <capitalize-string> x]
