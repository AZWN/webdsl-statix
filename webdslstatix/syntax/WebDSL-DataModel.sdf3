module WebDSL-DataModel

imports
  WebDSL-Action
  WebDSL-Lexical

context-free sorts

  EntityBodyDeclaration

context-free syntax

  Definition.GlobalsDefinition = "globals" "{" GlobalsDef* "}" {prefer, deprecated("'globals{ }' block is no longer necessary around global variables and functions")}
  Definition.Entity = "entity" Id ":" Id "{" EntityBodyDeclaration* "}" 
  Definition.EntityNoSuper = "entity" Id "{" EntityBodyDeclaration* "}" 
  Definition.SessionEntity = "session" Id "{" EntityBodyDeclaration* "}" 
  EntityBodyDeclaration = Property 
  EntityBodyDeclaration = Function 
  EntityBodyDeclaration = StaticEntityFunction 
  EntityBodyDeclaration.CachedEntity = "cache" 
  Definition.ExtendEntity = "extend" "entity" Id "{" EntityBodyDeclaration* "}" 
  Definition.ExtendSessionEntity = "extend" "session" Id "{" EntityBodyDeclaration* "}" 

context-free restrictions

  "cache" -/- [a-zA-Z0-9\_]

context-free sorts

  Property Sort Annotation GlobalsDef GlobalVarDecl

context-free syntax

  GlobalVarDecl.GlobalVarDecl = "var" Id ":" Sort 
  GlobalVarDecl.GlobalVarDeclInit = "var" Id ":" Sort ":=" Exp 
  GlobalVarDecl.GlobalVarDeclInitInferred = "var" Id ":=" Exp 
  GlobalVarDecl.GlobalVarDeclDepr = "var" Id ":" Sort ";" {deprecated("remove ';' from global variable declaration")}
  GlobalVarDecl.GlobalVarDeclInitDepr = "var" Id ":" Sort ":=" Exp ";" {deprecated("remove ';' from global variable declaration")}
  GlobalVarDecl.GlobalVarDeclInitInferredDepr = "var" Id ":=" Exp ";" {deprecated("remove ';' from global variable declaration")}
  Definition.RequestScope = "request" GlobalVarDecl 
  GlobalsDef = GlobalVarDecl 
  GlobalsDef = Function 
  Definition = GlobalsDef 

context-free sorts

  OptPropAnnos PropKind

context-free syntax

  Property.Property = Id PropKind Sort OptPropAnnos 
  Property.DerivedProperty = Id PropKind Sort OptPropAnnos ":=" Exp 
  OptPropAnnos = "(" {Annotation ","}* ")"
  OptPropAnnos.OptPropAnnosNone = <>
  PropKind.Simple = "::" 
  PropKind.Ref = "->" 
  PropKind.Comp = "<>" 
  PropKind.AnyProp = ":" 

context-free syntax

  Annotation.SimpleAnno = Id 
  Annotation.InverseAnno = "inverse" "=" Id "." Id 
  Annotation.IncompleteInverseAnno = "inverse" "=" Id 
  Annotation.InverseSlaveAnno = "inverseSlave" "=" Id "." Id 
  Annotation.InverseReferenceOnlyAnno = "inverse-reference-only" 
  Annotation.InlineAnno = "inline" "(" {Id ","}* ")" 
  Annotation.SelectAnno = "select" "=" Id 
  Annotation.NotNullAnno = "not null" 
  Annotation.NotEmptyAnno = "not empty" 
  Annotation.AllowedAnno = "allowed" "=" Exp 
  Annotation.DefaultAnno = "default" "=" Exp 
  Annotation.LengthAnno = "length" "=" Int 
  Annotation.FormatAnno = "format" "=" StringValue
  Annotation.IndexAnno = "index" "(" ")" 
  Annotation.IndexAnno = "index" "(" Int ")" 
  Annotation.CollationAnno = "collation" "(" Id ")" 
  Annotation.IdErrorAnno = "iderror" "=" StringValue
  Annotation.IdEmptyErrorAnno = "idemptyerror" "=" StringValue
