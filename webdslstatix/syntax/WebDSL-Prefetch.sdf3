module WebDSL-Prefetch

imports
  WebDSL-Lexical
  WebDSL-Action
  WebDSL-UI
  WebDSL

context-free sorts

  PrefetchFor PrefetchChildren PrefetchNode PrefetchTemplateArgs PrefetchNodeMod PrefetchCondition PrefetchCondVal PrefetchCond PrefetchWhere PrefetchWhereMod

context-free syntax

  PrefetchFor.PrefetchFor = "prefetch-for" PrefetchForVar PrefetchTemplateArgs PrefetchWhere PrefetchChildren 
  PrefetchChildren.PrefetchChildren = "{" PrefetchNode* "}" 
  PrefetchChildrenOpt.None = <>
  PrefetchChildrenOpt = PrefetchChildren 
  TemplateElement = PrefetchFor 
  Statement = PrefetchFor 
  PrefetchNode.PrefetchNode = OptPrefetchOwnerCast Id PrefetchNodeMod* PrefetchTemplateArgs PrefetchCondition PrefetchWhere PrefetchChildrenOpt 
  PrefetchIfDefault.None = <>
  PrefetchForVar.PrefetchForVar = Id 
  PrefetchNodeMod.NoEmptyBatch = "no-empty-batch" 
  PrefetchNodeMod.FetchEarly = "fetch-early" 
  TemplateArgOrThis.TemplateArgOrThis = Id "(" {SortOrThis ","}* ")" 
  SortOrThis = Sort 
  SortOrThis = PrefetchThis {prefer}
  SortOrThis.PrefetchThisCast = PrefetchThis "as" Sort 
  PrefetchThis.PrefetchThis = "this" 
  PrefetchTemplateArgs.None = <>
  PrefetchTemplateArgs.PrefetchTemplateArgs = "templates" "[" {TemplateArgOrThis ","}+ "]" 
  PrefetchCondition.None = <>
  PrefetchCondition.PrefetchCondition = "if" "(" PrefetchCondOrTrue ")" 
  PrefetchWhere.None = <>
  PrefetchWhere.PrefetchWhere = "where" PrefetchWhereMod* "(" PrefetchCondOrTrue ")" 
  PrefetchWhereMod.Hint = "hint" 
  NoneCons.None = <>
  PrefetchOwnerCast = SimpleSort "."
  OptPrefetchOwnerCast.None = <>
  OptPrefetchOwnerCast.Some = PrefetchOwnerCast
  PrefetchCondOrTrue = PrefetchCond
  PrefetchCondVal.PrefetchTrue = "true" {prefer}
  PrefetchCondVal.PrefetchFalse = "false" {prefer}
  PrefetchCondVal.PrefetchNull = "null" {prefer}
  PrefetchCondVal = ConstValue {prefer}
  PrefetchCondVal.PretchCondVar = Id 
  PrefetchCondVal.RelativeFieldAccess = NoneCons "." Id 
  PrefetchCondVal.RelativeFieldAccess = SimpleSort "." Id 
  PrefetchCondVal.PrefetchMul = PrefetchCondVal "*" PrefetchCondVal {assoc}
  PrefetchCondVal.PrefetchDiv = PrefetchCondVal "/" PrefetchCondVal {assoc}
  PrefetchCondVal.PrefetchMod = PrefetchCondVal "%" PrefetchCondVal {assoc}
  PrefetchCondVal.PrefetchAdd = PrefetchCondVal "+" PrefetchCondVal {assoc}
  PrefetchCondVal.PrefetchSub = PrefetchCondVal "-" PrefetchCondVal {assoc}
  PrefetchCond = PrefetchCondVal 
  PrefetchCond.PrefetchEq = PrefetchCondVal "==" PrefetchCondVal 
  PrefetchCond.PrefetchNotEq = PrefetchCondVal "!=" PrefetchCondVal 
  PrefetchCond.PrefetchLargerThan = PrefetchCondVal ">" PrefetchCondVal 
  PrefetchCond.PrefetchLargerThanOrEqual = PrefetchCondVal ">=" PrefetchCondVal 
  PrefetchCond.PrefetchSmallerThan = PrefetchCondVal "<" PrefetchCondVal 
  PrefetchCond.PrefetchSmallerThanOrEqual = PrefetchCondVal "<=" PrefetchCondVal 
  PrefetchCond.PrefetchAnd = PrefetchCond "&&" PrefetchCond {assoc}
  PrefetchCond.PrefetchOr = PrefetchCond "||" PrefetchCond {assoc}
  PrefetchCond.PrefetchNot = "!" PrefetchCond 
  PrefetchCond = "(" PrefetchCond ")" {bracket}
