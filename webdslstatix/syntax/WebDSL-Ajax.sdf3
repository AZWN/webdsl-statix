module WebDSL-Ajax

imports
  WebDSL-Action
  WebDSL-Lexical
  WebDSL-UI

sorts

  AjaxStatement Placeholder PlaceholderWithAjaxCall PlaceholderVar

context-free syntax

  Statement.AjaxStatement = AjaxStatement ";" {prefer}
  AjaxStatement.AjaxReplace = "replace" "(" Exp "," TemplateCall ")" 
  AjaxStatement.AjaxAppend = "append" "(" Exp "," TemplateCall ")" 
  AjaxStatement.AjaxVisibility = "visibility" "(" Exp "," AjaxVisibility ")" 
  AjaxVisibility.Show = "show" 
  AjaxVisibility.Toggle = "toggle" 
  AjaxVisibility.Hide = "hide" 
  AjaxStatement.AjaxRelocate = "relocate" "(" PageCall ")" 
  AjaxStatement.AjaxRestyle = "restyle" "(" Exp "," Exp ")" 
  AjaxStatement.AjaxClear = "clear" "(" Exp ")" 
  AjaxStatement.AjaxRefresh = "refresh" "(" ")" 
  AjaxStatement.AjaxRunScript = "runscript" "(" Exp ")" {prefer}
  
  TemplateElement = Placeholder
  TemplateElement = PlaceholderWithAjaxCall
  Placeholder.Placeholder = "placeholder" "<" Id ">" Id "{" TemplateElement* "}" {prefer}
  Placeholder.Placeholder = "placeholder" "<" Id ">" Exp "{" TemplateElement* "}" 
  Placeholder.Placeholder = "placeholder" Id "{" TemplateElement* "}" {prefer}
  Placeholder.Placeholder = "placeholder" Exp "{" TemplateElement* "}" 
  PlaceholderWithAjaxCall.PlaceholderWithAjaxCall = "placeholder" "<" Id ">" Id TemplateCall {prefer}
  PlaceholderWithAjaxCall.PlaceholderWithAjaxCall = "placeholder" "<" Id ">" PHExp TemplateCall 
  PlaceholderWithAjaxCall.PlaceholderWithAjaxCall = "placeholder" Id TemplateCall {prefer}
  PlaceholderWithAjaxCall.PlaceholderWithAjaxCall = "placeholder" PHExp TemplateCall 
  
  TemplateElement = PlaceholderVar 
  PlaceholderVar.PlaceholderVar = "placeholdervar" Id {prefer}

context-free syntax

  PHExp.ThisCall = Id "(" {Exp ","}* ")" 
  PHExp.Call = Exp "." Id "(" {Exp ","}* ")" 
  PHExp.FieldAccess = Exp "." Id {prefer}
  PHExp.CollectionIndex = Exp "[" Exp "]" {prefer}
  PHExp.Add = Exp "+" Exp 
  PHExp = ConstValue 
