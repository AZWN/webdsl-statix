module WebDSL-UI

imports
  WebDSL-Action
  WebDSL-Attributes
  WebDSL-Core
  WebDSL-Lexical
  WebDSL-XML

context-free sorts

  TemplateDefinition Modifier TemplateElement OptFilter PageCall TemplateCall PropertyAssignment
  TemplateArgExp TemplateCaseAlt TemplateTypeCaseAlt OptTemplateArgs TemplateArg OptLocalRedefineArgs
  LocalRedefineArg

lexical sorts

  SubmitProp

context-free syntax

  Definition.TemplateDefinition = TemplateDefinition
  TemplateDefinition.Define = <
    define <Modifier*> <Id> <OptFormalArgs> <OptTemplateArgs> {
      <TemplateElement*>
    }
  >
  OptTemplateArgs.TemplateArgs = <requires <{TemplateArg ","}*>>
  OptTemplateArgs.OptTemplateArgsNone = <>
  TemplateArg.TemplateArg = <<Id> ( <{Sort ","}*> )>
  TemplateDefinition.LocalRedefine = <
    define <Modifier*> <Id> ( <{FormalArg ","}*> ) = <Id> <OptLocalRedefineArgs>
  >
  OptLocalRedefineArgs.OptLocalRedefineArgsNone = <>
  OptLocalRedefineArgs.LocalRedefineArgs = <( * , <{LocalRedefineArg ","}+> )>
  LocalRedefineArg.LocalRedefineArg = Id
  TemplateDefinition.DefinePage = <
    <Modifier*> page <PageId> <OptFormalArgs> <OptTemplateArgs> {
      <TemplateElement*>
    }
  >
  TemplateDefinition.DefineTemplate = <
    <Modifier*> template <Id> <OptFormalArgs> <OptTemplateArgs> {
      <TemplateElement*>
    }
  >
  TemplateDefinition.DefineEmail = <
    <Modifier*> email <Id> <OptFormalArgs> <OptTemplateArgs> {
      <TemplateElement*>
    }
  >
  Modifier.Page = <page>
  Modifier.Email = <email>
  Modifier.AjaxTemplate = <ajax>
  Modifier.Template = <template>
  Modifier.Feed = <feed>
  Modifier.Local = <local>
  Modifier.NoSpan = <no-span>
  Modifier.TemplateModSpan = <span>
  Modifier.TemplateModInline = <inline>
  Modifier.TempateModSecure = <secure>
  Modifier.TemplateModNotSecure = <not-secure>
  Modifier.TemplateModOverride = <override>
  Modifier.TemplateDeprecated = <deprecated>
  Modifier.ExpireCache = <expire-cache>
  Modifier.ReadOnlyPage = <read-only>
  Modifier.WebService = <service>
  TemplateElement = InitAction
  TemplateElement = Action

context-free sorts

  VarDecl

context-free syntax

  VarDecl.TemplateVarDecl = <
    var <Id> : <Sort>
  >
  VarDecl.TemplateVarDeclDepr = <
    var <Id> : <Sort> ;
  > {deprecated("remove ;")}
  VarDecl.TemplateVarDeclInit = <
    var <Id> : <Sort> := <Exp>
  >
  VarDecl.TemplateVarDeclInitDepr = <
    var <Id> : <Sort> := <Exp> ;
  > {deprecated("remove ;")}
  VarDecl.TemplateVarDeclInitInferred = <
    var <Id> := <Exp>
  >
  VarDecl.TemplateVarDeclInitInferredDepr = <
    var <Id> := <Exp> ;
  > {deprecated("remove ;")}
  TemplateElement = VarDecl
  TemplateElement.RequestScopeTemplate = <
    request <VarDecl>
  >
  TemplateElement.LocalScopeTemplate = <
    local <VarDecl>
  >
  TemplateElement = TemplateDefinition
  TemplateElement = TemplateCall

context-free sorts

  Filter OrderExp Limit

context-free syntax

  Filter.FilterNoOrderByNoLimit = <where <Exp>>
  Filter.FilterNoWhereNoLimit = <order by <{OrderExp ","}+>>
  Filter.FilterNoLimit = <where <Exp> order by <{OrderExp ","}+>>
  Filter.FilterNoOrderBy = <where <Exp> <Limit>>
  Filter.FilterNoWhere = <order by <{OrderExp ","}+> <Limit>>
  Filter.FilterNoWhereNoOrderBy = Limit
  Filter.Filter = <where <Exp> order by <{OrderExp ","}+> <Limit>>
  OrderExp.OrderNonSpecific = Exp
  OrderExp.OrderAscending = <<Exp> asc>
  OrderExp.OrderDescending = <<Exp> desc>
  Limit.Limit = <limit <Exp> offset <Exp>>
  Limit.LimitNoOffset = <limit <Exp>>
  Limit.LimitNoLimit = <offset <Exp>>
  OptFilter.OptFilterSome = Filter
  OptFilter.OptFilterNone = <>

context-free sorts

  ForSeparator ElseIfTempl ElseTempl OptElseTempl

context-free syntax

  TemplateElement.For = <
    for ( <Id> : <Sort> in <Exp> <OptFilter> ) {
      <TemplateElement*>
    } <ForSeparator>
  >
  TemplateElement.ForInferred = <
    for ( <Id> in <Exp> <OptFilter> ) {
      <TemplateElement*>
    } <ForSeparator>
  >
  TemplateElement.ForAll = <
    for ( <Id> : <Sort> <OptFilter> ) {
      <TemplateElement*>
    } <ForSeparator>
  >
  TemplateElement.ForCount = <
    for ( <Id> : Int from <Exp> to <Exp> ) {
      <TemplateElement*>
    } <ForSeparator>
  >
  ForSeparator.ForSeparator = <separated-by { <TemplateElement*> }>
  ForSeparator.ForSeparatorNone = <>
  TemplateElement.IfElseIfTempl = <
    if ( <Exp> ) {
      <TemplateElement*>
    } <ElseIfTempl*> <OptElseTempl>
  >
  OptElseTempl.OptElseTemplNone = <>
  OptElseTempl.OptElseTemplSome = ElseTempl
  ElseIfTempl.ElseIfTempl = <else if ( <Exp> ) {
    <TemplateElement*>
  }>
  ElseTempl.ElseTempl = <else {
    <TemplateElement*>
  }>
  TemplateElement.TemplateCase = <
    case ( <{Exp ","}+> ) {
      <TemplateCaseAlt*>
    }
  >
  TemplateCaseAlt.TemplateCaseAlt = <
    <{Exp ","}+> { <TemplateElement*> }
  >
  TemplateCaseAlt.TemplateCaseAltDefault = <
    default { <TemplateElement*> }
  >
  TemplateElement.TemplateTypeCase = <
    typecase ( <Exp> as <Id> ) {
      <TemplateTypeCaseAlt*>
    }
  >
  TemplateTypeCaseAlt.TemplateTypeCaseAlt = <
    <Sort> { <TemplateElement*> }
  >
  TemplateTypeCaseAlt.TemplateTypeCaseDefault = <
    default { <TemplateElement*> }
  >

  // conditional templates are desugared to this constructor:
  TemplateElement.IfTempl = <
    if ( <Exp> ) {
      <TemplateElement*>
    } else {
      <TemplateElement*>
    }
  >

context-free sorts

  OptTCallArgs OptTCallPropAssigns TemplateBody ArgDefine

context-free syntax

  TemplateCall.TemplateCall = <
    <TemplateCallId> <OptTCallArgs> <OptTCallPropAssigns> <TemplateBody>
  >
  OptTCallArgs = <( <{TemplateArgExp ","}*> )>
  OptTCallArgs.OptTCallArgsNone = <>
  TemplateArgExp.Exp = Exp
  TemplateArgExp.TemplateElementsArgument = <{ <TemplateElement*> }>
  OptTCallPropAssigns = <[ <{PropertyAssignment ","}*> ]>
  OptTCallPropAssigns.OptTCallPropAssignsNone = <>
  TemplateBody.TemplateBody = <{
    <TemplateElement*>
  }>
  TemplateBody.TemplateBodyNone = <>
  TemplateBody.TemplateWith = <with {
    <ArgDefine*>
  }>
  ArgDefine.ArgDefine = <
    <TemplateCallId> ( <{FormalArg ","}*> ) {
      <TemplateElement*>
    }
  >
  TemplateCall = <with { <TemplateElement*> }> {reject}

context-free syntax

  TemplateElement.NavigateCallParenthesesNoProps = <
    navigate ( <PageCall> ) { <TemplateElement*> }
  >
  TemplateElement.NavigateCallParentheses = <
    navigate ( <PageCall> ) [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  TemplateElement.NavigateCallNoProps = <
    navigate <PageCall> { <TemplateElement*> }
  >
  TemplateElement.NavigateCall = <
    navigate <PageCall> [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  PageCall.PageCall = <
    <PageId> ( <{Exp ","}*> )
  >

context-free sorts

  ActionCallOrInline ActionCall InlineAction

context-free syntax

  TemplateElement.SubmitActionNoProps = <
    action ( <Exp> , <ActionCallOrInline> )
  >
  TemplateElement.SubmitAction = <
    action ( <Exp> , <ActionCallOrInline> ) [ <{PropertyAssignment ","}*> ]
  >
  TemplateElement.SubmitLinkActionNoProps = <
    actionLink ( <Exp> , <ActionCallOrInline> )
  >
  TemplateElement.SubmitLinkAction = <
    actionLink ( <Exp> , <ActionCallOrInline> ) [ <{PropertyAssignment ","}*> ]
  >
  TemplateElement.SubmitNoProps = <
    submit ( <Exp> , <ActionCallOrInline> )
  >
  TemplateElement.Submit = <
    submit ( <Exp> , <ActionCallOrInline> ) [ <{PropertyAssignment ","}*> ]
  >
  TemplateElement.SubmitLinkNoProps = <
    submitlink ( <Exp> , <ActionCallOrInline> )
  >
  TemplateElement.SubmitLink = <
    submitlink ( <Exp> , <ActionCallOrInline> ) [ <{PropertyAssignment ","}*> ]
  >
  TemplateElement.SubmitElemNoProps = <
    submit <ActionCallOrInline> { <TemplateElement*> }
  >
  TemplateElement.SubmitElem = <
    submit <ActionCallOrInline> [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  TemplateElement.SubmitLinkElemNoProps = <
    submitlink <ActionCallOrInline> { <TemplateElement*> }
  >
  TemplateElement.SubmitLinkElem = <
    submitlink <ActionCallOrInline> [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  TemplateElement.DownloadLinkNoProps = <
    downloadlink <ActionCallOrInline> { <TemplateElement*> }
  >
  TemplateElement.DownloadLink = <
    downloadlink <ActionCallOrInline> [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  TemplateElement.OutputImageNoProps = <
    outputimage <ActionCallOrInline> { <TemplateElement*> }
  >
  TemplateElement.OutputImage = <
    outputimage <ActionCallOrInline> [ <{PropertyAssignment ","}*> ] { <TemplateElement*> }
  >
  ActionCallOrInline = ActionCall
  ActionCallOrInline = InlineAction
  ActionCall.ActionCall = <<Id> ( <{Exp ","}*> )>
  InlineAction.InlineAction = <action <Block>>
  Sort = <action> {reject}
  TemplateCall = <action> {reject}
  Exp = <action> {reject}
  PropertyValue = <action> {reject}

context-free sorts

  PropertyValue ActionCallOrInlineOrExp

context-free syntax

  PropertyAssignment.PropertySubmitColon = <<SubmitProp> : <ActionCallOrInline>>
  PropertyAssignment.PropertySubmitAssign = <<SubmitProp> := <ActionCallOrInline>>
  PropertyAssignment.PropertySubmit = <<SubmitProp> = <ActionCallOrInline>>
  PropertyAssignment.PropertyAssignmentColon = <<ExtendedId> : <PropertyValue>>
  PropertyAssignment.PropertyAssignmentAssign = <<ExtendedId> := <PropertyValue>>
  PropertyAssignment.PropertyAssignment = <<ExtendedId> = <PropertyValue>>
  PropertyValue = Exp
  PropertyAssignment.TemplateCallPropertyAjax = <ajax>
  PropertyAssignment.TemplateCallPropertyNoLoadingFeedback = <no loading feedback>
  PropertyAssignment.TemplateCallPropertySecure = <secure>
  PropertyAssignment.TemplateCallPropertyIgnoreValidation = <ignore-validation>
  PropertyAssignment.TemplateCallPropertyNotSecure = <not-secure>
  PropertyAssignment.TemplateCallPropertyNotNull = <not null>
  PropertyAssignment = CommonAttribute
  PropertyAssignment.PropertySubmitMultiple = <<SubmitProp> = <{ActionCallOrInlineOrExp ";"}+>>
  ActionCallOrInlineOrExp = ActionCallOrInline
  ActionCallOrInlineOrExp = PropertyValue
  TemplateElement = OutputShorthand

syntax

  TemplateElement-CF.Text = "\"" StringLex-LEX "\""
  TemplateElement-CF.TextInterp = "\"" StringPart-CF* "\""

lexical syntax

  SubmitProp = "onabort"
  SubmitProp = "onblur"
  SubmitProp = "onchange"
  SubmitProp = "onclick"
  SubmitProp = "ondblclick"
  SubmitProp = "onerror"
  SubmitProp = "onfocus"
  SubmitProp = "onkeydown"
  SubmitProp = "onkeypress"
  SubmitProp = "onkeyup"
  SubmitProp = "onload"
  SubmitProp = "onmousedown"
  SubmitProp = "onmousemove"
  SubmitProp = "onmouseout"
  SubmitProp = "onmouseover"
  SubmitProp = "onmouseup"
  SubmitProp = "onreset"
  SubmitProp = "onresize"
  SubmitProp = "onselect"
  SubmitProp = "onsubmit"
  SubmitProp = "onunload"
  SubmitProp = "oninput"

context-free sorts

  InitAction ActionModifier TemplateCallId

context-free syntax

  TemplateCall = <separated-by { <TemplateElement*> }> {reject}
  Definition = InitAction
  InitAction.InitAction = <init <Block>>
  TemplateElement.DataBindAction = <databind <Block>>
  TemplateElement.RenderAction = <render <Block>>
  TemplateElement.AllPhasesAction = <allphases <Block>>
  TemplateElement.ValidateAction = <validate <Block>>
  Action.Action = <action <ActionModifier*> <Id> <OptFormalArgs> <Block>>
  ActionModifier.ActionModifierIgnoreValidation = <ignore-validation>
  TemplateCallId.QualifiedElementsCall = <<TemplateCallIdLex> .elements>
  TemplateCallId = TemplateCallIdLex

context-free sorts

  FormalVarArg ExpVarArg

context-free syntax

  Sort.TemplateVarArgSort = <[ <{FormalVarArg ","}+> ]>
  FormalVarArg.FormalVarArg = <<Id> : <Sort>>
  TemplateCall.MemberTemplateCall = <
    <Id> . <TemplateCall>
  >
  Exp.TemplateVarArgExp = <[ <{ExpVarArg ","}+> ]>
  ExpVarArg.ExpVarArg = <( <{Exp ","}*> )>
  ExpVarArg.ExpVarArgFor = <for ( <Id> : <Sort> in <Exp> <OptFilter> ) { <{ExpVarArg ","}*> }>
  ExpVarArg.ExpVarArgForInferred = <for ( <Id> in <Exp> <OptFilter> ) { <{ExpVarArg ","}*> }>
  ExpVarArg.ExpVarArgForAll = <for ( <Id> : <Sort> <OptFilter> ) { <{ExpVarArg ","}*> }>
  ExpVarArg.ExpVarArgForCount = <for ( <Id> : Int from <Exp> to <Exp> ) { <{ExpVarArg ","}*> }>

context-free sorts

  HTMLWrapper HTMLWrapperAssigns HTMLWrapperBody

context-free syntax

  HTMLWrapper.HTMLWrapper = <
    htmlwrapper <Id> <Id> <HTMLWrapperAssigns>
  >
  HTMLWrapperAssigns.HTMLWrapperAssignsCommaSep = <[
    <{Attribute ","}*>
  ]>
  HTMLWrapperAssigns.HTMLWrapperAssigns = <[
    <Attribute*> ]
  >
  HTMLWrapperAssigns.HTMLWrapperAssignsNone = <>
  TemplateDefinition = HTMLWrapper
  TemplateDefinition.HTMLWrapperMultiple = <
    htmlwrapper {
      <HTMLWrapperBody*>
    }
  >
  HTMLWrapperBody.HTMLWrapperBody = <<Id> <Id> <HTMLWrapperAssigns>>

context-free sorts

  TemplateId

context-free syntax

  TemplateId.TemplateId = <id>
  Exp = TemplateId

lexical syntax

  VarId = "id" {reject}

context-free restrictions

  TemplateId -/- [a-zA-Z0-9\_]
