module WebDSL-AccessControl 

imports
  WebDSL-Action
  WebDSL-Core
  WebDSL-DataModel
  WebDSL-Lexical

context-free sorts

  // Optional sorts
  OptMatchArgs OptId OptWildCardArg

  // Access control sorts
  AccessControlDefinition AccessControlRule
  AccessControlPointcutElement AccessControlPointcut AccessControlImportRulesArg
  PolicyExp Predicate MatchArgs

lexical sorts

  MatchId WildCardArg AccessControlRuleType

lexical syntax

  MatchId = [a-zA-Z] [a-zA-Z0-9\_\-]* [\*]? 
  MatchId = [\*] 
  WildCardArg = [\*] 
  AccessControlRuleType = "page"
  AccessControlRuleType = "action"
  AccessControlRuleType = "template"
  AccessControlRuleType = "ajaxtemplate"
  AccessControlRuleType = "pointcut"

context-free syntax

  Section.AccessControlDefinition = "access" "control" "rules" OptId AccessControlDefinition* 
  Section.AccessControlDefinitionAlt = "access" "control" "rules" "{" OptId AccessControlDefinition* "}"
  OptId.OptIdNone = <>
  OptId.OptIdSome = Id
  Definition.AccessControlPrincipalDef                = "principal" "is" Id "with" "credentials" {Id ","}+
  AccessControlDefinition.AccessControlPrincipalAcDef = "principal" "is" Id "with" "credentials" {Id ","}+
  AccessControlDefinition.Rule = AccessControlRule 
  AccessControlRule.AccessControlRule = "rule" AccessControlRuleType MatchId OptMatchArgs "{" Exp AccessControlRule* "}"
  AccessControlRule.SpecialAccessControlRule = "rule" "logsql" "{" Exp "}"
  OptMatchArgs.OptMatchArgsSome = "(" MatchArgs ")"
  OptMatchArgs.OptMatchArgsNone = <>
  MatchArgs.MatchArgs = {FormalArg ","}* OptWildCardArg {prefer}
  MatchArgs.MatchArgsExps = {Exp ","}* OptWildCardArg
  Predicate.Predicate = "predicate" Id OptFormalArgs "{" Exp "}"
  Definition = Predicate
  AccessControlDefinition = Predicate
  EntityBodyDeclaration.PredicateInEntity = "predicate" Id OptFormalArgs "{" Exp "}" 
  AccessControlPointcutElement.AccessControlPointcutElement = Id MatchId "(" {Id ","}* OptWildCardArg ")" 
  AccessControlPointcut.AccessControlPointcut = "pointcut" Id "(" {FormalArg ","}* ")" "{" {AccessControlPointcutElement ","}* "}" 
  AccessControlDefinition = AccessControlPointcut 
  TemplateElement.AccessControlImportRules = "apply ac rules" Id "(" {AccessControlImportRulesArg ","}* ")" {prefer}
  AccessControlImportRulesArg.ACVar = Id 

  OptWildCardArg.OptWildCardArgNone = <>
  OptWildCardArg.OptWildCardArgSome = WildCardArg

context-free syntax

  Section.ACPolicy = "access" "control" "policy" PolicyExp 
  PolicyExp.Name = Id 
  PolicyExp = "(" PolicyExp ")" {bracket}
  PolicyExp.PolicyAnd = PolicyExp "AND" PolicyExp {assoc}
  PolicyExp.PolicyOr = PolicyExp "OR" PolicyExp {assoc}

context-free syntax

  Modifier.IgnoreAccessControl = "ignore-access-control" 
