module WebDSL-Expand

imports
  WebDSL-Lexical
  WebDSL-UI

context-free sorts

  DeriveProperty DeriveBody DeriveBodyElement

context-free syntax

  TemplateElement.DeriveNoBody = <
    derive <Id> from <Exp> for ( <{DeriveProperty ","}*> )
  > {prefer}
  TemplateElement.DeriveNoForNoBody = <
    derive <Id> from <Exp>
  > {prefer}
  TemplateElement.Derive = <
    derive <Id> from <Exp> for ( <{DeriveProperty ","}*> ) <DeriveBody>
  > {prefer}
  TemplateElement.DeriveNoFor = <
    derive <Id> from <Exp> <DeriveBody>
  > {prefer}
  TemplateElement.DeriveNoFromNoFor = <
    derive <Id> <DeriveBody>
  > {prefer}

  DeriveProperty.DeriveDefault = Id
  DeriveProperty.DeriveMode = <<Id> ( <Id> )>
  DeriveBody.DeriveBody = <{
    <DeriveBodyElement*>
  }>
  DeriveBodyElement.DeriveBodyElement = <
    <Id> { <TemplateElement*> }
  >

  TemplateCall = <derive> {reject}
  Definition.DeriveCrud = <derive crud <Id>> {prefer}
  Definition.DeriveCrudUpper = <derive CRUD <Id>> {prefer}

context-free syntax

  Definition.ExpandTemplateCall = <
    expand <ExpandId+> to <Id>
  >
  Definition.ExpandTemplateDefinition = <
    expandtemplate <Id> to <ExpandArgId+> {
      <Definition*>
    }
  >
  TemplateElement.TemplateElementExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> {
      <TemplateElement*>
    }
  > {prefer}
  EntityBodyDeclaration.EntityBodyDeclarationExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> {
      <EntityBodyDeclaration*>
    }
  > {prefer}
  Statement.StatementExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> {
      <Statement*>
    }
  > {prefer}
  ObjectPropertyAssignment.ObjectPropertyAssignmentExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> {
      <ObjectPropertyAssignment*>
    }
  > {prefer}
  Definition.DefinitionExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> {
      <Definition*>
    }
  > {prefer}
  Exp.ExpExpandLocal = <expand <ExpandId+> to <ExpandArgId+> { <Exp> }> {prefer}
  Attribute.AttributeExpandLocal = <expand <ExpandId+> to <ExpandArgId+> { <Attribute> }> {prefer}
  PropertyAssignment.PropertyAssignmentExpandLocal = <expand <ExpandId+> to <ExpandArgId+> { <PropertyAssignment> }> {prefer}
  FormalArg.FormalArgExpandLocal = <expand <ExpandId+> to <ExpandArgId+> { <FormalArg> }> {prefer}
  Case.CaseAltExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> { <Case> }
  > {prefer}
  TemplateCaseAlt.TemplateCaseAltExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> { <TemplateCaseAlt> }
  > {prefer}
  TypeCaseAlt.TypeCaseAltExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> { <TypeCaseAlt> }
  > {prefer}
  TemplateTypeCaseAlt.TemplateTypeCaseAltExpandLocal = <
    expand <ExpandId+> to <ExpandArgId+> { <TemplateTypeCaseAlt> }
  > {prefer}

  ExpandId = <expandtemplate> {reject}
  ExpandId = <template> {reject}
  ExpandId = <expand> {reject}
  ExpandId = <to> {reject}
  ExpandArgId = <expandtemplate> {reject}
  ExpandArgId = <template> {reject}
  ExpandArgId = <expand> {reject}
  ExpandArgId = <to> {reject}

lexical sorts

  ExpandId ExpandArgId

lexical syntax

  ExpandId = [a-zA-Z0-9\_\-]+ 
  ExpandArgId = Id 

lexical restrictions

  ExpandId -/- [a-zA-Z0-9\_\-]
